# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <https://github.com/blacksky3>
#Credits: Jan de Groot <jgc@archlinux.org>
#Credits: Eric Engestrom <eric@engestrom.ch>
#Credits: Lone_Wolf <lone_wolf@klaas-de-kat.nl>

pkgname=lib32-libdrm-git
pkgdesc='Userspace interface to kernel DRM services (32-bit) (git version)'
pkgver=libdrm.2.4.107.r19.g02ac4a0a
pkgrel=1
arch=(i686 x86_64)
url='http://dri.freedesktop.org/'
license=(custom)
makedepends=(libxslt docbook-xsl meson git ninja valgrind gcc gcc-libs lib32-libxslt lib32-gcc-libs)
checkdepends=(cairo lib32-cairo cunit)
depends=(lib32-libpciaccess libdrm)
conflicts=(lib32-libdrm)
provides=(lib32-libdrm lib32-libdrm-git)
source=(git+https://gitlab.freedesktop.org/mesa/drm.git
        COPYING)
md5sums=(SKIP
         SKIP)

pkgver(){
  cd ${srcdir}/drm

  # cutting off 'foo-' prefix that presents in the git tag
  git describe --long --tags --abbrev=7 | sed 's/^libdrm.//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
export CC='gcc -m32'
export CXX='g++ -m32'
export ASFLAGS=--32
export CFLAGS=-m32
export CXXFLAGS=-m32
export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  cd ${srcdir}/drm

  # remove build dir if there is one
  if dir build_32; then
    rm -rf -v build_32
  fi

  # create build dir
  mkdir -p -v build_32

  meson setup build_32 \
  -D b_ndebug=true \
  -D buildtype=plain \
  --wrap-mode=nofallback \
  -Dprefix=/usr \
  -D sysconfdir=/etc \
  --libdir=/usr/lib32 \
  -Dintel=enabled \
  -Dradeon=enabled \
  -Damdgpu=enabled \
  -Dnouveau=enabled \
  -Dvmwgfx=enabled \
  -Domap=enabled \
  -Dexynos=enabled \
  -Dfreedreno=enabled \
  -Dtegra=enabled \
  -Dvc4=enabled \
  -Detnaviv=enabled \
  -Dcairo-tests=disabled \
  -Dman-pages=disabled \
  -Dvalgrind=disabled \
  -Dfreedreno-kgsl=true \
  -Dinstall-test-programs=true \
  -Dudev=true \
  -Dtests=true

  #meson configure build_32/

  ninja -C build_32

  meson compile -C build_32
}

package_lib32-libdrm-git(){
  DESTDIR="$pkgdir" ninja -C ${srcdir}/drm/build_32/ install

  rm -rf "${pkgdir}"/usr/{include,share,bin}

  # installing license
  install -Dt "$pkgdir"/usr/share/licenses/"$pkgname" -m644 COPYING
}
